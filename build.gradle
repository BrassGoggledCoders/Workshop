import groovy.json.JsonOutput
import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "maven"
apply plugin: "java"
apply plugin: "idea"

version = "${minecraft_version}-" + getVersion()
group = "${group}"
archivesBaseName = "${mod_name}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            include '**/**'
            //Add the generated main module resources
            srcDirs += 'src/datagen/generated/'
            //But exclude the cache of the generated data from what gets built
            exclude '.cache'
        }
    }
    //Data gen modules for the different modules
    datagenmain { setupDataGenSourceSet(datagenmain, 'main') }
}

def setupDataGenSourceSet(SourceSet datagenSourceSet, String name) {
    datagenSourceSet.java.srcDirs = ['src/datagen/' + name + '/java']
    datagenSourceSet.resources.srcDirs = ['src/datagen/' + name + '/resources']
    //datagenSourceSet.compileClasspath += project.sourceSets.api.output
    datagenSourceSet.compileClasspath += project.sourceSets.main.output
}

configurations {
    datagenmainCompile.extendsFrom(compile)
    datagenmainImplementation.extendsFrom(implementation)
    datagenmainCompileOnly.extendsFrom(compileOnly)
    datagenmainRuntimeOnly.extendsFrom(runtimeOnly)
}

def version_properties = [
        "mod_version"       : mod_version,
        "titanium_version"    : titanium_version,
        "forge_version" : forge_version,
        "loader_version": loader_version
]

def jsonPatterns = ["**/*.json", "**/*.mcmeta"]

processResources {
    outputs.upToDateWhen { false }
    from(sourceSets.main.resources) {
        include "META-INF/mods.toml"
        expand version_properties
    }
    into "$buildDir/resources/main/"
    //Minify json resources
    doLast {
        fileTree(dir: getOutputs().getFiles().getAsPath(), includes: jsonPatterns).each {
            File file -> file.setText(JsonOutput.toJson(new JsonSlurper().parse(file)))
        }
    }
}

minecraft {
    mappings channel: "snapshot", version: "${mappings_version}"

    runs {
        client {
            workingDirectory project.file("run")
            source sourceSets.main
            properties 'mixin.env.disableRefMap': 'true'
        }

        server {
            workingDirectory project.file("run")
            source sourceSets.main
            properties 'mixin.env.disableRefMap': 'true'
        }

        data {
            workingDirectory project.file("run")
            environment 'target', 'fmluserdevdata'
            properties 'fml.earlyprogresswindow': 'false'
            properties 'mixin.env.disableRefMap': 'true'

            args '--all', '--output', file('src/datagen/generated/'),
                    '--mod', 'workshop', '--reports',
                    '--existing', sourceSets.main.resources.srcDirs[0]

            //datagenSourceSet.compileClasspath += project.sourceSets.main.output
            mods {
                workshop.sources((SourceSet[]) [sourceSets.main, sourceSets.datagenmain])
            }
        }
    }
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

repositories {
    mavenLocal()
    maven {
        url 'https://cdn.hrzn.studio/maven/'
    }
    maven {
        url 'https://maven.blamejared.com'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    if (findProject(':Titanium') != null) {
        compile project(':Titanium')
    } else {
        compile fg.deobf (project.dependencies.create("com.hrznstudio:titanium:${minecraft_version}-${titanium_version}") {
            transitive = false
        })
    }
    compile fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
    compile fg.deobf("vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}")
    //compile fg.deobf("xyz.brassgoggledcoders:PatchouliProvider:1.15.2-1.0.0-Snapshot.5")
}

jar {
    manifest {
        attributes([
            "Specification-Title"     : "${title}",
            "Specification-Vendor"    : "${team}",
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${version}",
            "Implementation-Vendor"   : "${team}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "FMLAT": "accesstransformer.cfg"
        ])
    }
}

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if (branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if (buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if (buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}
